apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'
apply from: "${rootDir}/publish-configuration.gradle"

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        repositories {
            maven {
                name "sonatype"
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username rootProject.ext["ossrhUsername"]
                    password rootProject.ext["ossrhPassword"]
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }
                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = 'TremblingPreventAppbarBehavior'
                    description = 'TremblingPreventAppbarBehavior'
                    url = 'https://github.com/CaptainWonJong/TremblingPreventAppbarBehavior'
                    licenses {
                        license {
                            name = 'DoingLab License'
                            url = 'https://github.com/doinglab/FoodLensSDK/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'CaptainWonJong'
                            name = 'CaptainWonJong'
                            email = 'CaptainWonJong@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/CaptainWonJong/TremblingPreventAppbarBehavior.git'
                        developerConnection = 'scm:git:ssh://github.com/CaptainWonJong/TremblingPreventAppbarBehavior.git'
                        url = 'https://github.com/CaptainWonJong/TremblingPreventAppbarBehavior'
                    }
                }
            }
        }
        signing {
            useInMemoryPgpKeys(
                    rootProject.ext["signing.keyId"],
                    rootProject.ext["signing.key"],
                    rootProject.ext["signing.password"],
            )
            sign publishing.publications
        }
    }
}